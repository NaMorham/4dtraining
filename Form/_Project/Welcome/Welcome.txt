  //%attributes = {"lang":"en"} comment added and reserved by 4D.
  //Project Method: Welcome
  //
  //   Parameter        Type             Description
  //
  //Description:
  //  
  //
  //Date        Init  Description
  //===============================================================================
  //27/04/2017   AH   Initial version

DBG_BeginMethod (Current method name;True)

C_LONGINT($vL_FormEvent)
$vL_FormEvent:=Form event

C_BOOLEAN($vB_continue)
$vB_continue:=True

Case of
	: ($vL_FormEvent=On Load)
		DBG_Log ("Event = On Load")
	
		If (<>vL_WelcomeDialogProcID<1)
			<>vL_WelcomeDialogProcID:=Current process
		Else
			$vB_continue:=False
			CANCEL
			BRING TO FRONT(<>vL_WelcomeDialogProcID)
		End if
	
		If ($vB_continue)
			C_POINTER($vP_WelcomeLbl)
		
			If (<>vL_testProcessID<1)
				<>vL_testProcessID:=New process("__test_timer";16*1024;"Background timer process";*)
			Else
				DBG_Log ("__test_timer process is already running")
			End if
		
			$vP_WelcomeLbl:=OBJECT Get pointer(Object named;"WelcomeText_Lbl")
			If (Nil($vP_WelcomeLbl))
				ALERT("Could not get welcome label pointer")
			Else
				$vP_WelcomeLbl->:="\n\nWelcome to\n\n"+String(Current date;System date long)
				OBJECT SET VERTICAL ALIGNMENT($vP_WelcomeLbl;Align center)
			End if
		End if
	
	: ($vL_FormEvent=On Clicked)
		DBG_Log ("Event = On Clicked")
	
		  // handle the buttons
		Case of
			: (vB_closeWelcomeBtn=1)
				<>vB_isQuitting:=True
				If (<>vL_UtilitiesDialogProcID>=1)
					CALL PROCESS(<>vL_UtilitiesDialogProcID)
				End if
			
				<>vB_bg_continue:=False
				CANCEL
			
			: (vB_showEmployeesBtn=1)
				EVT_ShowEmployees
			
			: (vB_showMeetingsBtn=1)
				EVT_ShowMeetings
			
		End case
	
	: ($vL_formEvent=On Close Box)
		DBG_Log ("Event = On Close Box")
	
		<>vB_isQuitting:=True
		If (<>vL_UtilitiesDialogProcID>=1)
			CALL PROCESS(<>vL_UtilitiesDialogProcID)
		End if
	
		<>vB_bg_continue:=False
		CANCEL
	
	Else
		DBG_Log ("Event = "+String($vL_formEvent))
	
End case

DBG_EndMethod
