  //%attributes = {"lang":"en"} comment added and reserved by 4D.
  //Project Method: EMP_ShowDetailForm({$vT_empID})
  //
  //   Parameter        Type             Description
  //    $vT_empID       Text      <-     The UUID of the employee to show, or "" to
  //                                     create a new employee
  //
  //Description:
  //  
  //
  //Date        Init  Description
  //===============================================================================
  //19/05/2017   AH   Initial version.

DBG_BeginMethod (Current method name)

C_TEXT($1;$vT_empID)
$vT_empID:=""

Case of
	: (Count parameters<1)
	: ($1="")
		  // Do nothing - new employee
	
	Else
		$vT_empID:=$1
End case

  // Check if we need a new process first
If (<>vL_editEmployeeFormProcID<1)
	  // There is no existing process, create a new one and call ourselves again
	<>vL_editEmployeeFormProcID:=New process("EMP_ShowDetailForm";32*1024;"EmployeeDetail";$vT_empID)
Else
	  // no we have a valid proc id, we can have our proc variables
	If (Type(vT_employeeUUID)=Is undefined)
		C_TEXT(vT_employeeUUID)
	End if

	C_BOOLEAN($vB_continue;$vB_newEmployee)
	$vB_continue:=True
	$vB_newEmployee:=True

	If ($vT_empID#"")
		$vB_newEmployee:=False
		vT_employeeUUID:=$vT_empID
	End if

	If ($vB_newEmployee)
		vT_employeeUUID:=""
	Else
		QUERY([Employee];[Employee]ID=$vT_empID)
		If (OK#1)
			$vB_continue:=False
			vT_employeeUUID:=[Employee]ID
		End if
	End if

	If ($vB_continue)
		If (<>vL_editEmployeeFormID<1)
			<>vL_editEmployeeFormID:=Open form window([Employee];"EmployeeDetail")
			DIALOG([Employee];"EmployeeDetail")
			CLOSE WINDOW(<>vL_editEmployeeFormID)
			<>vL_editEmployeeFormID:=-1
			<>vL_editEmployeeFormProcID:=-1
		Else
			If (<>vL_editEmployeeFormProcID>=1)
				BRING TO FRONT(<>vL_editEmployeeFormProcID)
				REDRAW WINDOW(<>vL_editEmployeeFormID)
			Else
				DBG_Log ("No valid process ID for EmployeeDetail window")
				$vB_continue:=False
			End if
		End if
	Else
		DBG_Log ("Could not process employee detail window")
	End if
End if

DBG_EndMethod