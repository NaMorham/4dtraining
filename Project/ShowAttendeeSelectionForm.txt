  //%attributes = {"lang":"en"} comment added and reserved by 4D.
  //Project Method: ShowAttendeeSelectionForm
  //
  //   Parameter        Type             Description
  //
  //Description:
  //   
  //
  //Date        Init  Description
  //===============================================================================
  //31/05/2017   AH   Initial version.

DBG_BeginMethod (Current method name)

C_TEXT($1;$vT_mtgID)
$vT_mtgID:=""

C_BOOLEAN($vB_continue)
$vB_continue:=False

Case of 
	: (Count parameters<1)
	: ($1="")
		  // Do nothing - new meeting
		
	Else 
		$vT_mtgID:=$1
		$vB_continue:=True
End case 

If ($vB_continue)
	If (<>vL_attendeeSelectionFormProcID<1)
		  // we need to create (and register - todo) the new process
		<>vL_attendeeSelectionFormProcID:=New process("ShowAttendeeSelectionForm";32*1024*1024;"SelectAttendeesForm";$vT_mtgID)
		
	Else 
		QUERY([Meeting];[Meeting]ID=$vT_mtgID)
		If (OK#1)
			DBG_Log ("Cannot query for a meeting with ID ["+$vT_mtgID+"]")
			$vB_continue:=False
		Else 
			DBG_Log ("Found existing meeting with ID ["+[Meeting]ID+"]")
		End if 
		
		If ($vB_continue)
			If (Type(vT_meetingUUID)=Is undefined)
				C_TEXT(vT_meetingUUID)
			End if 
			vT_meetingUUID:=$vT_mtgID
			
			If (<>vL_attendeeSelectionFormID<1)
				<>vL_attendeeSelectionFormID:=Open form window("SelectAttendeesForm";Movable form dialog box)
				DIALOG("SelectAttendeesForm")
				CLOSE WINDOW(<>vL_attendeeSelectionFormID)
				<>vL_attendeeSelectionFormID:=-1
				<>vL_attendeeSelectionFormProcID:=-1
				REDRAW WINDOW(<>vL_mainFormID)
				
			Else 
				If (<>vL_attendeeSelectionFormProcID>=1)
					BRING TO FRONT(<>vL_attendeeSelectionFormProcID)
					REDRAW WINDOW(<>vL_attendeeSelectionFormID)
				Else 
					DBG_Log ("No valid process ID for Attendee selection window")
					$vB_continue:=False
				End if 
			End if 
		Else 
			DBG_Log ("Could not process Attendee selection window")
		End if 
		
	End if 
	
Else 
	DBG_Log ("No meeting selected for Attendee selection")
End if 

DBG_EndMethod 
