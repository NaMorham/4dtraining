  //%attributes = {"lang":"en"} comment added and reserved by 4D.
  //Project Method: MAINFRM_PopulateEmplolyeesList
  //
  //   Parameter        Type             Description
  //
  //Description:
  //
  //
  //Date        Init  Description
  //===============================================================================
  //18/05/2017   AH   Initial version.

  // Sanity check

If (Type(aT_empListUUID)=Is undefined)
	ARRAY TEXT(aT_empListUUID;0)
End if
If (Type(aT_empNameCol)=Is undefined)
	ARRAY TEXT(aT_empNameCol;0)
End if
If (Type(aB_managerCol)=Is undefined)
	ARRAY BOOLEAN(aB_managerCol;0)
End if
If (Type(aT_emailCol)=Is undefined)
	ARRAY TEXT(aT_emailCol;0)
End if
If (Type(aT_mobileCol)=Is undefined)
	ARRAY TEXT(aT_mobileCol;0)
End if
If (Type(aD_startedCol)=Is undefined)
	ARRAY TEXT(aD_startedCol;0)
End if

C_POINTER($vP_employeesList)
$vP_employeesList:=OBJECT Get pointer(Object named;"employeeList_LB")

If (Nil($vP_employeesList))
	DBG_Log ("Could not get pointer to employees list")
End if

  // 1. clear any old data from each of the arrays
MCSU_ARRAY_Clear (->aT_empListUUID;->aT_empNameCol;->aB_managerCol;->aT_emailCol;->aT_mobileCol;->aD_startedCol)

  // 2. get selection
  //    2.1. Preserve existing Selection details (UUID)
C_TEXT($vT_selectedUUID)
$vT_selectedUUID:=[Employee]ID

C_LONGINT($vL_numInSelection)
$vL_numInSelection:=Records in selection([Employee])

If ($vL_numInSelection>=1)
	DBG_Log (String($vL_numInSelection)+" employees in selection")
	FIRST RECORD([Employee])

	  // 3. For each record;
	C_LONGINT($vL_listIndex)
	$vL_listIndex:=1

	  // TODO: Should this be a for loop instead
	While (Not(End selection([Employee])))
		APPEND TO ARRAY(aT_empListUUID;[Employee]ID)
		  //    3.1. Generate display name
		C_TEXT($vT_DisplayName)
		$vT_DisplayName:=Uppercase([Employee]Surname)+", "+[Employee]FirstName
		  //    3.2. Add display name to array
		  // ...
		APPEND TO ARRAY(aT_empNameCol;$vT_DisplayName)
		APPEND TO ARRAY(aB_managerCol;[Employee]IsManagerFg)
		APPEND TO ARRAY(aT_emailCol;[Employee]Email)
		APPEND TO ARRAY(aT_mobileCol;[Employee]Mobile)
		APPEND TO ARRAY(aD_startedCol;[Employee]StartDate)

		If (([Employee]StartDate<=Current date) & (([Employee]EndDate=!00-00-00!) | ([Employee]EndDate>Current date)))
			LISTBOX SET ROW COLOR($vP_employeesList->;$vL_listIndex;0x00FFFFFF)  // they are employed
		Else

			If ([Employee]StartDate>Current date)
				LISTBOX SET ROW COLOR($vP_employeesList->;$vL_listIndex;0x00AFAFAF)  // they have not started yet

			Else
				LISTBOX SET ROW COLOR($vP_employeesList->;$vL_listIndex;0x00FFAFAF)  // or are no longer employed

			End if

		End if

		NEXT RECORD([Employee])
		$vL_listIndex:=$vL_listIndex+1
	End while


	  // 4. Find currently selected user (if required)
	If ($vT_selectedUUID#"")
		  //    4.1. Set index value on all arrays

	End if
Else
	DBG_Log ("No employees in selection")
End if
